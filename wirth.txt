programa = "prog" "{" declaracoes comandos "}" [{funcao}] .
declaracoes = declaracao {declaracao} .
declaracao = tipo identificador ";" .
comandos = comando {comando} .
comando = atribuicao | condicao | iteracao | chamadafuncao | entrada | saida | retorno .
tipo = "int" | "char" | "string" | "float" | "bool" .
atribuicao = identificador "=" expressao ";" .
operando = numero | identificador .
expressao = exparitmetica | caractere | string | expbool .
exparitmetica = operando [{operadorarit operando}] .
operadorarit = "+" | "-" | "*" | "/" .
operadorlogico = "==" | "<=" | ">=" | "<" | ">" | "!=" .
expbool = "verdadeiro" | "falso" .
condicao = "se" "("expressaocond")" "{"comandos"}" ["senao" "{"comandos"}"] .
expressaocond = expressao operadorlogico expressao {operadorcond expressao operadorlogico expressao} .
operadorcond = "&&" | "||" .
iteracao = "enquanto" "("expressaocond")" "{"comandos"}" .
chamadafuncao = identificador "(" [identificador {"," identificador}] ")" ";" .
funcao = tipo "funcao" identificador "("argumentos")" "{"comandos retorno"}" .
argumentos = tipo identificador {"," tipo identificador} .
retorno = "retorno" [identificador] ";" .
entrada = "entrada" identificador ";" .
saida = "saida" identificador ";" .